<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="POU" Id="{e8522887-71a4-4b27-ae5e-ab8551e36bc8}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_Director
VAR_INPUT
    CondicionInicial: BOOL; // Condiciones materiales necesarias para iniciar la produccion
    CondicionMarcha: BOOL; // Condiciones logicas necesarias para iniciar la produccion
    Emergencia: BOOL;
    Finalizada: BOOL;
    FinTarea: BOOL;
    FinCiclo: BOOL;
    Manual: BOOL;
    Marcha: BOOL;
    ParaCiclo: BOOL;
    Preparada: BOOL;
    Reinicia: BOOL;
END_VAR
VAR_OUTPUT
    Modo: E_GEMMA := E_GEMMA.A6;

    Defecto: BOOL; // Procedimientos de defecto
    Funcionamiento: BOOL; // Procedimientos de funcionamiento
    Parada: BOOL; // Procedimientos de parada
    Produccion: BOOL; // Procedimientos que proporcionan valor añadido
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[// FUNCION DE ESTADO
IF Emergencia THEN
    Modo := E_GEMMA.D1;
ELSIF Reinicia THEN
    Modo := E_GEMMA.A6;
ELSE
    CASE Modo OF
        // Procedimientos de parada
        E_GEMMA.A1: // Parada en el estado inicial
            IF NOT CondicionInicial THEN
                Modo := E_GEMMA.A6;
            ELSIF Manual THEN
                Modo := E_GEMMA.F4;
            ELSIF (CondicionMarcha AND Marcha) THEN
                Modo := E_GEMMA.F2;
            END_IF
        E_GEMMA.A2: // Parada solicitada a final de ciclo
            IF FinCiclo THEN
                Modo := E_GEMMA.A1;
            END_IF
        E_GEMMA.A3: // Parada solicitada en un estado determinado 
            ;
        E_GEMMA.A4: // Parada obtenida 
            ;
        E_GEMMA.A5: // Preparando de reanudacion tras el fallo 
            ;
        E_GEMMA.A6: // Situando la PO en el estado inicial 
            IF CondicionInicial THEN
                Modo := E_GEMMA.A1;
            ELSIF Manual THEN
                Modo := E_GEMMA.F4;
            END_IF
        E_GEMMA.A7: // Situando la PO en un estado determinado 
            ;

        // Procedimientos de funcionamiento 
        E_GEMMA.F1: // Produccion normal 
            IF FinTarea THEN
                Modo := E_GEMMA.F3;
            ELSIF ParaCiclo THEN
                Modo := E_GEMMA.A2;
            END_IF
        E_GEMMA.F2: // Secuencia de preparacion 
            IF Preparada THEN
                Modo := E_GEMMA.F1;
            END_IF
        E_GEMMA.F3: // Secuencia de finalizacion 
            IF Finalizada THEN
                Modo := E_GEMMA.A1;
            END_IF
        E_GEMMA.F4: // Verificacion sin orden 
            IF NOT Manual THEN
                Modo := E_GEMMA.A6;
            END_IF
        E_GEMMA.F5: // Verificacion en orden 
            ;
        E_GEMMA.F6: // Prueba 
            ;

        // Procedimientos de defecto 
        E_GEMMA.D1: // Parada de emergencia 
            IF NOT Emergencia THEN
                Modo := E_GEMMA.A6;
            END_IF
        E_GEMMA.D2: // Diagnostico/tratamiento de los fallos 
            ;
        E_GEMMA.D3: // Produccion a pesar de los fallos 
            ;
    END_CASE
END_IF

// PARAMETROS DE SALIDA 
Defecto := (Modo >= E_GEMMA.D1) AND (Modo <= E_GEMMA.D3);
Funcionamiento:= (Modo >= E_GEMMA.F1) AND (Modo <= E_GEMMA.F6);
Parada := (Modo >= E_GEMMA.A1) AND (Modo <= E_GEMMA.A7);
Produccion := (Modo = E_GEMMA.A2) OR (Modo = E_GEMMA.A3) OR (Modo = E_GEMMA.F1) 
    OR (Modo = E_GEMMA.F2) OR (Modo = E_GEMMA.F3) OR (Modo = E_GEMMA.F5)
    OR (Modo = E_GEMMA.F6) OR (Modo = E_GEMMA.D3); 
]]></ST>
    </Implementation>
    <LineIds Name="POU">
      <LineId Id="32" Count="77" />
      <LineId Id="9" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>